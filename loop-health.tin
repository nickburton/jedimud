#VARIABLE HOURS_TNL {0};

#nop The {a} backlog needs to be put inside here
#nop simply because it will also be seen after the mobs.
#ACTION {[%0/%1H %2/%3M %4/%5V XP:%6]} {    
    #SHOWME $STATUS;

    #VARIABLE HP %0;
    #VARIABLE MP %2;
    #VARIABLE VP %4;
    
    #IF {$FIRST_EXP == 0} {
        #VARIABLE FIRST_EXP %6;
    };
    #ELSE {
        #FORMAT currenttime %T;
        #IF {($currenttime - $starttime) >= 60} {
          #MATH {TOTAL_EXP} {$FIRST_EXP - %6};  
          #MATH {HOURLY_EXP} {$TOTAL_EXP / ($currenttime - $starttime) * 60 * 60 / 1000};
          #MATH {HOURS_TNL} {%6 / $HOURLY_EXP / 1000};  
        };
    };

    #MATH {HP_PCT} {%0 / %1 * 1.0};
    #MATH {MP_PCT} {%2 / %3 * 1.0};
    #MATH {VP_PCT} {%4 / %5 * 1.0};

    #SHOWME [XP/HR(K): $HOURLY_EXP (HRS: $HOURS_TNL)];

    #IF {$VP_PCT < $VP_MIN_PCT && $CAN_REJU} {
        reju
    };

    #IF {%6 < $AT_XP_SWITCH_TO_LEVEL_GEAR && "$SWITCHED_LEVEL_GEAR" == "FALSE"} {
        con-gear;
        #var SWITCHED_LEVEL_GEAR TRUE;
        #2 drinkme;
        feedme;
    };

    #IF {"$STATUS" != "HEALING" && "$FOLLOWING" == "TRUE"} {
        #IF {$HP_PCT < $HP_MIN_PCT && $MP > $COST_CRSR && $CAN_HEAL} {
            crsr;
            #VARIABLE STATUS HEALING;
            #bell
        };
    };
    #ELSEIF {"$STATUS" == "HEALING"} {
        #VARIABLE STATUS READY
    };

    #IF {$LOOP == 0 && $KILL == 0} {
        #return
    };

    #IF {$HP_PCT < $HP_MIN_PCT} {
        #VARIABLE NEED_HP 1;
        #IF {$HP_PCT < $CRITICAL_HP_PCT && "$ASKED_FOR_HEAL" == "FALSE" } {
            say Need HEAL;
            #var ASKED_FOR_HEAL TRUE;
        };
        #ELSEIF {"$STATUS" == "READY" || "$STATUS" == "MOVING"} {
            sleep
        };

    };
    #ELSE {
        #VARIABLE NEED_HP 0
    };

    #IF {"$STATUS" != "ATTACKING" && "$STATUS" != "SLEEPING" && "$STATUS" != "HEALING"} {
        #IF {$HP_PCT < $HP_MIN_PCT || $MP_PCT < $MP_GBL_MIN_PCT || $VP_PCT < $VP_MIN_PCT} {
            #IF {$VP_PCT < $VP_MIN_PCT && $CAN_REJU} {
                reju
            };
            #ELSE {
                sleep
            };
        };
    };
    #ELSEIF {"$STATUS" == "SLEEPING"} {
        #IF {$HP_PCT == 1.0 && $MP_PCT > $MP_MIN_PCT && $VP_PCT > $VP_MIN_PCT} {
            wake
        };
    };
    #ELSEIF {"$STATUS" != "HEALING"} { 
        #nop Leader or Follower heals itself;
        #IF {$HP_PCT < $HP_MIN_PCT && $MP > $COST_CRSR && $CAN_HEAL} {
            crsr;
            #VARIABLE STATUS HEALING;
        };
    };
}