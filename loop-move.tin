#VARIABLE AUTO_RESTART 1;
#VARIABLE LAST_PATH {NIL}
#VARIABLE LOOP {0}

#ALIAS {start} {
    #var {LAST_PATH} %1;
    #var {stats} {};
    #path load %1;
    rego;
}

#ALIAS {stop} {
    #VARIABLE LOOP 0;
    #VARIABLE KILL 0;
    #VARIABLE LOCK 0;
    #VARIABLE STATUS READY;
    #UNTICKER {looker};
    #UNTICKER {mover};
    #DELAY {2} {
        #VARIABLE {STATUS} {READY};
    }
}

#ALIAS {rego} {
    #SHOWME Regoing...;
    #VARIABLE STATUS READY;
    #VARIABLE LOOP 1;
    #VARIABLE KILL 1;
    #VARIABLE LOCK 0;
    look;
    start-looker;
}

#ALIAS {check-loop} {
    #IF {$LOOP} {
        #VARIABLE STATUS READY;
        #VARIABLE LOCK 0;
        #VARIABLE HAVE_UNCN 0;
        start-looker;
    }
}

#ALIAS {start-looker} {
    #DELAY {1} {
        #TICKER {looker} {
            #IF {"$STATUS" != "ATTACKING" && "$STATUS" != "BLESSING" && "$STATUS" != "SLEEPING"} {
                #SHOWME Looking...;
                #VARIABLE STATUS READY;
                look
            };
        } {4};
    }
}

#ALIAS {wake} {
    stand;
    #variable STATUS READY;
    check-food;
    check-water;
    check-bless;
    wake-gear;
    #IF {$LOOP} {
        start-looker
    };
    #unticker {sleep-check};
}

#ALIAS {sleep} {
    #if {"$STATUS" != "SLEEPING" && "$STATUS" != "ATTACKING"}
    {
        #variable STATUS SLEEPING;
        #showme $STATUS;
        sleep-gear;
        rest;
        #ticker {sleep-check} 
        {
            #cr;
            #variable STATUS SLEEPING;
        } {10}
    }
}

#ACTION {You feel a lot better!} {
    #IF {"$STATUS" == "HEALING"} {#VARIABLE STATUS HEALED};
    #IF {"$LEADING" == "TRUE" && "$STATUS" != "ATTACKING"} {wake};
}

#ACTION {You feel much better!} {
    #IF {"$STATUS" == "HEALING"} {#VARIABLE STATUS HEALED};
    #IF {"$LEADING" == "TRUE" && "$STATUS" != "ATTACKING"} {wake};
}

#ACTION {That player is not here.} { rego }

#ACTION {[ Exits:} {
    #UNTICKER mover;
	#TICKER {mover} {
        #IF {$LOOP && ("$STATUS" == "READY" || "$STATUS" == "MOVING")} {
            #VARIABLE STATUS MOVING;
            #SHOWME ++++Walking++++;
            #PATH describe;
            #PATH walk;
        };    
    } {2};
}

#ACTION {END OF PATH} {
    stop;
    #IF {$AUTO_RESTART} {start $LAST_PATH}
}